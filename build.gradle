buildscript {
	ext {
		springBootVersion = '2.0.2.RELEASE'
		versionJQuery = '2.1.4'
		versionBootstrap = '3.3.4'
		versionJasmine = '2.3.4'
	}
	repositories {
		maven{ url 'http://maven.aliyun.com/nexus/content/groups/public/'}
//		mavenCentral()
	}
	dependencies {
		classpath 'ro.isdc.wro4j.gradle:wro4j-gradle-plugin:1.8.0.Beta4'
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'wro4j'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'org.neu'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	maven{ url 'http://maven.aliyun.com/nexus/content/groups/public/'}
//		mavenCentral()
}

webResources {
    bundle ('core') {
        js 'js/**/*.js'
        preProcessor 'jsMin'
    }

    bundle ('libs') {
        js "webjars/jquery/$versionJQuery/jquery.min.js"
    }

    bundle ('theme-default') {
        css "webjars/bootstrap/$versionBootstrap/less/bootstrap.less"
        css 'themes/default/main.css'

        cssOverrideImport "variables.less", "../../../../themes/default/variables.less"
        preProcessor 'less4j'
        cssRewriteUrl()
    }

    assets {
        include 'themes/default/images/**'

        /* Uncomment the following lines if you prefer to copy fonts to your /static/themes/default/fonts */
        /*from ("$buildMainDir/webjars/bootstrap/$versionBootstrap/fonts") {
            include '**'
            into 'themes/default/fonts'
        }*/
    }

    testAssets {
        from (srcTestDir) {
            exclude '*SpecRunner.html'
            exclude '*.conf.js'
        }

        from (srcTestDir) {
            include '*SpecRunner.html'
            include '*.conf.js'

            expand([
                    'srcMain': buildMainUri,
                    'srcTest': buildTestUri,
                    'webjarJasmine': "$buildTestUri/webjars/jasmine/$versionJasmine"
            ])
        }
    }
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-security')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-websocket')
	runtime('mysql:mysql-connector-java')

	webjars 'org.webjars:jquery:2.1.4'
	webjars 'org.webjars:bootstrap:3.3.4'

	compileOnly('org.projectlombok:lombok')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework.security:spring-security-test')
}
